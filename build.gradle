plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id "org.flywaydb.flyway" version "6.2.1"
    id "com.moowork.node" version "1.3.1"
    id 'checkstyle'
//    id "com.github.spotbugs" version "4.0.5"
}

group 'org.example'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.11

apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    implementation "org.apache.commons:commons-lang3:${rev_common_langs}"
    implementation 'commons-validator:commons-validator:1.6'
    implementation "com.auth0:java-jwt:${rev_jwt}"
    implementation "org.flywaydb:flyway-core:${rev_flyway_version}"
    implementation "com.zaxxer:HikariCP:${rev_hikari_cp}"
    implementation "org.postgresql:postgresql:${rev_postgresql_connector}"
    implementation 'com.amazonaws:aws-java-sdk-ssm:1.11.816'
//    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation 'com.opentable.components:otj-pg-embedded:0.12.0'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion '8.29'
    config = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml')
}

task appNpmInstall(type: NpmTask) {
    workingDir = file("${rootProject.projectDir}/ui")
    args = ["install"]
}

task appNpmRunBuild(type: NpmTask, dependsOn: appNpmInstall) {
    workingDir = file("${rootProject.projectDir}/ui")
    args = ['run', 'build']
}

task copyWebApp(type: Copy, dependsOn: appNpmRunBuild) {
    from "${rootProject.projectDir}/ui/build"
    into "${rootProject.projectDir}/build/resources/main/static/."
}

compileJava.dependsOn copyWebApp
